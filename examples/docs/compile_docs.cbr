//this only works on Windows
dir := "C:\\dev\\cebra\\examples\\docs\\"

//*nix
//dir_nix := "/home/thomas/dev/cebra/examples/docs/"

//combine html files

f := open(dir + "cebra_docs_template.html")

out := open(dir + "compiled_docs.html")
clear(out)

while !eof(f) {
    s := read_line(f)
    if s.size > 0 and s[0] == "$"{
        about_f := open(dir + s[1:s.size])
        about_s := read_all(about_f)
        append(out, about_s + "\n")
        close(about_f)
    } else {
        append(out, s + "\n")
    } 
}

close(f)
close(out)

//highlight compiled_docs.html
//constants magenta
//types blue
//functions green
//comments grey

f = open(dir + "compiled_docs.html")
out = open(dir + "cebra_docs.html")
clear(out)

code_block := false
while !eof(f) {
    line := read_line(f)

    if start_code(line)    code_block = true
    if end_code(line)      code_block = false

    if code_block {
        line = highlight_line(line)
    }

    append(out, line + "\n")
}

close(f)
close(out)

Mode :: enum {
    STRING
    COMMENT
    IDENTIFIER
    NUMBER
    NONE
}

Highlight :: struct {
    start := -1
    end := -1
    color := ""
}

is_alpha_numeric :: (s: string) -> (bool) {
    -> is_alpha(s) or is_digit(s) or s == "_"
}

highlight_line :: (s: string) -> (string) {

    keywords := Map<bool>()
    keywords["for"] = true
    keywords["foreach"] = true
    keywords["if"] = true
    keywords["while"] = true
    keywords["else"] = true
    keywords["when"] = true
    keywords["is"] = true
    keywords["struct"] = true
    keywords["enum"] = true
    keywords["import"] = true
    keywords["as"] = true
    keywords["in"] = true

    keywords["clock"] = true
    keywords["print"] = true
    keywords["open"] = true
    keywords["close"] = true
    keywords["read_all"] = true
    keywords["read_line"] = true
    keywords["rewind"] = true
    keywords["clear"] = true
    keywords["eof"] = true
    keywords["append"] = true
    keywords["write"] = true
    keywords["input"] = true

    constants := Map<bool>()
    constants["true"] = true
    constants["false"] = true
    constants["nil"] = true

    types := Map<bool>()
    types["int"] = true
    types["float"] = true
    types["string"] = true
    types["bool"] = true
    types["File"] = true
    types["List"] = true
    types["Map"] = true

    //loop through string once and create Highlight objects and put it into a
    //list.  At the end, loop through the list and apply the css tags
    mode := Mode.NONE
    start := 0
    dq := "\""[1:2]

    highlights := List<Highlight>()

    i := 0
    while i < s.size {
        when mode {
            is Mode.NONE {
                if s[i] == "&" {
                    while s[i] != ";" {
                        i = i + 1 
                    } 
                }
                if s[i] == dq {
                    mode = Mode.STRING
                    start = i
                }
                if is_alpha(s[i]) or s[i] == "&" or s[i] == "#" {
                    mode = Mode.IDENTIFIER
                    start = i
                }
                if is_digit(s[i]) {
                    mode = Mode.NUMBER
                    start = i
                }
                if i > 0 and s[i-1] == "/" and s[i] == "/" and i > 1 and s[i-2] != ":" {
                    h := Highlight()
                    h.start = i - 1
                    h.end = s.size
                    h.color = "comment"
                    highlights[highlights.size] = h
                    i = s.size - 1//skipping remainder of string
                }
            }
            is Mode.STRING {
                if s[i] == dq {
                    mode = Mode.NONE
                    h := Highlight()
                    h.start = start
                    h.end = i + 1
                    h.color = "string"
                    highlights[highlights.size] = h
                } 
            }
            is Mode.IDENTIFIER {
                //if !(is_alpha_numeric(s[i]) or s[i] == "&" or s[i] == "#") {
                if !is_alpha_numeric(s[i]) {
                    mode = Mode.NONE
                    if s[start:i] in keywords.keys {
                        h := Highlight()
                        h.start = start
                        h.end = i
                        h.color = "keyword"
                        highlights[highlights.size] = h
                    }

                    if s[start:i] in constants.keys {
                        h := Highlight()
                        h.start = start
                        h.end = i
                        h.color = "constant"
                        highlights[highlights.size] = h
                    }
                    if s[start:i] in types.keys {
                        h := Highlight()
                        h.start = start
                        h.end = i
                        h.color = "type"
                        highlights[highlights.size] = h
                    }
                }
            }
            is Mode.NUMBER {
                if !(is_digit(s[i]) or s[i] == ".") {
                    mode = Mode.NONE
                    h := Highlight()
                    h.start = start
                    h.end = i
                    h.color = "constant"
                    highlights[highlights.size] = h
                } 
            }
        }
        i = i + 1
    }

    if mode == Mode.NUMBER {
        h := Highlight()
        h.start = start
        h.end = s.size
        h.color = "constant"
        highlights[highlights.size] = h
    }

    if mode == Mode.IDENTIFIER {
        if s[start:s.size] in keywords.keys {
            h := Highlight()
            h.start = start
            h.end = s.size
            h.color = "keyword"
            highlights[highlights.size] = h
        }

        if s[start:s.size] in constants.keys {
            h := Highlight()
            h.start = start
            h.end = s.size
            h.color = "constant"
            highlights[highlights.size] = h
        }
        if s[start:s.size] in types.keys {
            h := Highlight()
            h.start = start
            h.end = s.size
            h.color = "type"
            highlights[highlights.size] = h
        }
    }

    if highlights.size < 0 {
        print(s) print("\n")
        print("pairs: \n")
        for i := 0, i < highlights.size, i = i + 1 {
            print(highlights[i].start) print("   " ) print(highlights[i].end) print("\n")
        }
    }

    {
        result := "<span class=\"code\">"
        start := 0
        for i := 0, i < highlights.size, i = i + 1 {
            result = result + s[start : highlights[i].start] + 
                     "<span class=\"" + highlights[i].color + "\">" + 
                     s[highlights[i].start : highlights[i].end] + 
                     "</span>"
            start = highlights[i].end
        }
        -> result + s[start : s.size] + "</span>"
    }
}


start_code :: (s: string) -> (bool) {
  trimmed := remove_starting_whitespace(s)
  -> trimmed.size >= 6 and trimmed[0:6] == "<code>"
}

end_code :: (s: string) -> (bool) {
  trimmed := remove_starting_whitespace(s)
  -> trimmed.size >= 7 and trimmed[0:7] == "</code>"
}

remove_starting_whitespace :: (s: string) -> (string) {
    i := 0
    while i < s.size - 1 and s[i] == " " {
        i = i + 1
    }
    -> s[i:s.size]
}
